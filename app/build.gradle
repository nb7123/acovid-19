apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.host"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    def assetsDir = "build/generated/assets"
    def dynamicAssetsDir = "${assetsDir}/dynamic"
    def navgraphAssetsDir = "${assetsDir}/navgraph"
    sourceSets {
        debug {
            assets.srcDir("${navgraphAssetsDir}/debug/")
            assets.srcDir("${dynamicAssetsDir}/debug/")
        }

        release {
            assets.srcDir("${navgraphAssetsDir}/release/")
            assets.srcDir("${dynamicAssetsDir}/release/")
        }
    }
// To inline the bytecode built with JVM target 1.8 into
// bytecode that is being built with JVM target 1.6. (e.g. navArgs)


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    implementation "androidx.navigation:navigation-fragment:2.2.1"
    implementation 'androidx.navigation:navigation-ui:2.2.1'

    implementation project(path: ':constants')
    implementation project(path: ':dynamic:dynamic-fragment')
    implementation project(path: ':dynamic:dynamic-runtime')
}

def createCopyTask(dependsOn, buildType, destDir, destFileName, srcFile) {
    def destFile = file("${destDir}/${buildType}/${destFileName}")
    destFile.getParentFile().mkdirs()

    return tasks.create("copy${buildType.capitalize()}${destFileName.capitalize()}Task", Copy) {
        group = "build"
        description = "copy ${destFileName} to assets dir."
        from(srcFile.getParent()) {
            include(srcFile.name)
            rename { destFile.name }
        }
        into(destFile.getParent())
    }.dependsOn("${dependsOn}")
}

def generateApk(assembleTask, buildType) {
    def assetsDir = "build/generated/assets"
    def dynamicAssetsDir = "${assetsDir}/dynamic"
//    def navgraphAssetsDir = "${assetsDir}/navgraph"

//    // generate navigation graph apk
//    def projectName = "navgraph"
//    def buildDir = project(":${projectName}").getBuildDir().path
//
//    def navGraphApkFile = file(buildDir +
//            "/outputs/apk/${buildType}/" + "${projectName}-${buildType}.apk")
//
//    assembleTask.dependsOn createCopyTask(
//            ":${projectName}:assemble${buildType.capitalize()}",
//            buildType,
//            navgraphAssetsDir,
//            "${projectName}-${buildType}.apk",
//            navGraphApkFile)

    // generate dynamic apks
    def projectName = "plugin01"
    def buildDir = project(":${projectName}").getBuildDir().path
    def dynamic01ApkFile = file("${buildDir}" +
            "/outputs/apk/${buildType}/" + "${projectName}-${buildType}.apk")

    assembleTask.dependsOn createCopyTask(
            ":${projectName}:assemble${buildType.capitalize()}",
            buildType,
            dynamicAssetsDir,
            "${projectName}-${buildType}.apk",
            dynamic01ApkFile)
}

tasks.whenTaskAdded { task ->
    print("When task added: $task")
    if (task.name == "generateDebugAssets") {
        generateApk(task, "debug")
    }

    if (task.name == "generateReleaseAssets") {
        generateApk(task, "release")
    }
}
